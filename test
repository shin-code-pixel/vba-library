Private Sub RunCase(ByVal procFullName As String, ByRef res As TTestResult)
    res.Total = res.Total + 1

    Dim fullName As String
    fullName = "'" & ThisWorkbook.Name & "'!" & procFullName

    Mdl_TestState.VBL_TestCaseReset

    Err.Clear
    On Error Resume Next
    Application.Run fullName

    ' テスト本体が落ちた（予期せぬ実行時エラー）
    If Err.Number <> 0 Then
        res.Failed = res.Failed + 1
        Debug.Print " - FAIL  " & fullName
        Debug.Print "        Err=" & Err.Number & " " & Err.Description
        Err.Clear
        On Error GoTo 0
        Exit Sub
    End If

    ' Assertで失敗した
    If Mdl_TestState.gCaseFailed Then
        res.Failed = res.Failed + 1
        Debug.Print " - FAIL  " & fullName
        Debug.Print "        " & Mdl_TestState.gCaseFailMsg
    Else
        res.Passed = res.Passed + 1
        Debug.Print " - PASS  " & fullName
    End If

    On Error GoTo 0
End Sub
